int iButton
ref rRef

begin Function {iButton}

	; Load preset
	if iButton == 0

		; Set name
		Player.LNSetName (Goo1.AuxVarGetStr "CharPre_Cache" 0)

		; Set SPECIAL
		if eval !(IsModLoaded "TaleOfTwoWastelands.esm" && GetQR (EditorIDToFormID "CG00"))
			Player.SetAV Strength (Goo1.AuxVarGetFlt "CharPre_Cache" 1)
			Player.SetAV Perception (Goo1.AuxVarGetFlt "CharPre_Cache" 2)
			Player.SetAV Endurance (Goo1.AuxVarGetFlt "CharPre_Cache" 3)
			Player.SetAV Charisma (Goo1.AuxVarGetFlt "CharPre_Cache" 4)
			Player.SetAV Intelligence (Goo1.AuxVarGetFlt "CharPre_Cache" 5)
			Player.SetAV Agility (Goo1.AuxVarGetFlt "CharPre_Cache" 6)
			Player.SetAV Luck (Goo1.AuxVarGetFlt "CharPre_Cache" 7)
		endif

		; Set tag skills
		SetPlayerTagSkill (Goo1.AuxVarGetStr "CharPre_Cache" 8) 0
		SetPlayerTagSkill (Goo1.AuxVarGetStr "CharPre_Cache" 9) 1
		SetPlayerTagSkill (Goo1.AuxVarGetStr "CharPre_Cache" 10) 2

		; Mod support
		if eval IsModLoaded "TTW Quick Start.esp"
			rRef = GFFM "Fallout3.esm" "24720"
			rRef.SetVariable "specialdone" 0
			rRef.SetVariable "tagdone" 0
			rRef.SetVariable "namedone" 0
			rRef.SetVariable "traitdone" 0
		endif

		; Show trait warning message
		if Goo1.AuxVarType "CharPre_Cache" 11 == 4
			MessageBoxExAlt ({int iTemp} => "") "^Character Stat Presets^Preset with traits detected.%rOpen the traits menu to apply them.|%z" (GetStringSetting "sOk")
		endif

	; Randomize preset
	elseif iButton == 1 && Goo1.AuxVarGetFlt "*CharPre_Flt" 2 == 1
		Call (CompileScript "CharacterBuildPresets\PresetPrompt.gek") (Goo1.AuxVarGetFlt "*CharPre_Flt" 1)

	; Return to preset menu
	else
		Call (CompileScript "CharacterBuildPresets\LoadPrompt.gek")
	endif

end