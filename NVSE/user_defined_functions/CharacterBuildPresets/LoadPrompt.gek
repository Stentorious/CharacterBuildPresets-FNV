int iCount
int iLoop
int iTemp
int iVerify
string_var sFilename
string_var sMsgBox
array_var aPresets
array_var aINI
array_var aVerify
array_var aStat
array_var aForE

begin Function {}

	; Build message box string with available presets
	aPresets = GetFilesInFolder "CharacterBuildPresets" "*.ini"
	sMsgBox = sv_Construct "^%z^" (GetINIString "Strings:sTitlePreset" "Stentorious\Translate\CharacterBuildPresets.ini")
	Goo1.AuxVarErase "*CharPre_File"
	if aPresets
		aVerify = Ar_List "Agility", "Charisma", "Endurance", "Intelligence", "Luck", "Name", "Perception", "Strength", "TagSkills", "Traits"
		foreach aForE <- aPresets
			sFilename = *aForE

			; Verify INI formatting
			iVerify = 1
			aINI = GetINISection "Preset" ("..\CharacterBuildPresets\" + sFilename)

			; Verify [Preset] section
			if eval (Ar_Size aINI) == 0
				iVerify = 0
				PrintC "Malformed %z: Missing [Preset] section" sFilename
			elseif eval (Ar_Keys aINI) != aVerify
				iVerify = 0
				PrintC "Malformed %z: Incomplete [Preset] section" sFilename
			else

				; Verify tag skills
				aStat = Sv_Split (aINI["TagSkills"]) "|"
				iTemp = Ar_Size aStat
				if eval iTemp == 3
					iLoop = 0
					while iLoop < 3
						iTemp = StringToActorValue (aStat[iLoop])
						if eval iTemp < 32 || iTemp > 45
							iVerify = 0
							PrintC "Malformed %z: Unknown Skill %q%z%q" sFilename (aStat[iLoop])
						endif
						iLoop += 1
					loop
				elseif iTemp > 3
					iVerify = 0
					PrintC "Malformed %z: Too many tagged skills" sFilename
				else
					iVerify = 0
					PrintC "Malformed %z: Missing tagged skill(s)" sFilename
				endif

				; Verify SPECIAL points
				if eval Goo1.AuxVarGetFlt "*CharPre_INI" 4
					iTemp = #(aINI["Strength"]) + #(aINI["Perception"]) + #(aINI["Endurance"]) + #(aINI["Charisma"]) + #(aINI["Intelligence"]) + #(aINI["Agility"]) + #(aINI["Luck"]) - 7
					if eval iTemp > (iLoop = Goo1.AuxVarGetFlt "*CharPre_Flt" 0)
						iVerify = 0
						PrintC "Invalid %z: Excess SPECIAL points (%g > %g)" sFilename iTemp iLoop
					endif

					; Verify trait points
					aStat = Sv_Split (aINI["Traits"]) "|"
					iTemp = Ar_Size aStat
					if eval iTemp > (iLoop = GetGS iTraitMenuMaxNumTraits)
						iVerify = 0
						PrintC "Invalid %z: Excess trait points (%g > %g)" sFilename iTemp iLoop
					endif
				endif
			endif

			; Add preset option
			if iVerify
				Goo1.AuxVarSetStr "*CharPre_File" (sFilename) -1
				Sv_Replace ".ini|" sFilename
				sMsgBox += Sv_Construct "|%z" sFilename
				iCount += 1
			endif

		loop
	endif

	; Cache number of presets
	Goo1.AuxVarSetFlt "*CharPre_Flt" iCount 1

	; Show build presets message box
	MessageBoxExAlt (CompileScript "CharacterBuildPresets\PresetPrompt.gek") "%z|%z|%z" sMsgBox (GetStringSetting "sRandom") (GetStringSetting "sCancel")

	aPresets = aVerify = aINI = aStat = aForE = Ar_Null
	Sv_Destruct sMsgBox sFilename

end