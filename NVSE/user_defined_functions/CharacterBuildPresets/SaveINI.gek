string_var sFilename

int iSize
ref rPerk
string_var sINI
array_var aPerks
array_var aForE

begin Function {sFilename}

	sFilename = Sv_Construct "..\CharacterBuildPresets\%z.ini" sFilename

	; Character name
	SetINIString "Preset:Name" (Player.LNGetName) (sFilename)

	; SPECIAL
	SetINIFloat "Preset:Strength" (Player.GetBaseActorValue Strength) (sFilename)
	SetINIFloat "Preset:Perception" (Player.GetBaseActorValue Perception) (sFilename)
	SetINIFloat "Preset:Endurance" (Player.GetBaseActorValue Endurance) (sFilename)
	SetINIFloat "Preset:Charisma" (Player.GetBaseActorValue Charisma) (sFilename)
	SetINIFloat "Preset:Intelligence" (Player.GetBaseActorValue Intelligence) (sFilename)
	SetINIFloat "Preset:Agility" (Player.GetBaseActorValue Agility) (sFilename)
	SetINIFloat "Preset:Luck" (Player.GetBaseActorValue Luck) (sFilename)

	; Tag skills
	if eval IsPlayerTagSkill Barter
		sINI += "|Barter"
	endif
	if eval IsPlayerTagSkill BigGuns
		sINI += "|BigGuns"
	endif
	if eval IsPlayerTagSkill EnergyWeapons
		sINI += "|EnergyWeapons"
	endif
	if eval IsPlayerTagSkill Explosives
		sINI += "|Explosives"
	endif
	if eval IsPlayerTagSkill Lockpick
		sINI += "|Lockpick"
	endif
	if eval IsPlayerTagSkill Medicine
		sINI += "|Medicine"
	endif
	if eval IsPlayerTagSkill MeleeWeapons
		sINI += "|MeleeWeapons"
	endif
	if eval IsPlayerTagSkill Repair
		sINI += "|Repair"
	endif
	if eval IsPlayerTagSkill Science
		sINI += "|Science"
	endif
	if eval IsPlayerTagSkill Guns
		sINI += "|Guns"
	endif
	if eval IsPlayerTagSkill Sneak
		sINI += "|Sneak"
	endif
	if eval IsPlayerTagSkill Speech
		sINI += "|Speech"
	endif
	if eval IsPlayerTagSkill Survival
		sINI += "|Survival"
	endif
	if eval IsPlayerTagSkill Unarmed
		sINI += "|Unarmed"
	endif
	sINI[0] = ""
	SetINIString "Preset:TagSkills" (sINI) (sFilename)

	; Traits
	aPerks = Player.GetAllPerks
	if aPerks
		sINI = ""
		foreach aForE <- aPerks
			if eval IsTrait (rPerk = (*aForE))
				sINI += Sv_Construct "|%z" (GetEditorID rPerk)
			endif
		loop
		sINI[0] = ""
		SetINIString "Preset:Traits" (sINI) (sFilename)
	else
		SetINIFloat "Preset:Traits" 0 (sFilename)
	endif

	aPerks = aForE = Ar_Null
	Sv_Destruct sFilename sINI

end