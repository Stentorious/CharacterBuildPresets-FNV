int iButton

int iLoop
int iRand
int iSize
int iPatch
ref rPerk
string_var sINI
string_var sMsgBox
string_var sPreset
array_var aStat
array_var aNames
array_var aForE

; Preset
string_var sName
int iStrength
int iPerception
int iEndurance
int iCharisma
int iIntelligence
int iAgility
int iLuck

begin Function {iButton}

	Goo1.AuxVarSetFlt "*CharPre_Flt" 0 2

	; Saved preset
	if iButton < Goo1.AuxVarGetFlt "*CharPre_Flt" 1
		Goo1.AuxVarSetFlt "*CharPre_Flt" iButton 1

		; Build message box string with character stats
		sPreset = (Goo1.AuxVarGetStr "*CharPre_File" iButton)
		sINI = Sv_Construct "..\CharacterBuildPresets\%z" sPreset
		Sv_Replace ".ini|" sPreset

		; Preset/character name
		PrintC "Loading Build Preset: %z" sPreset
		sName = GetINIString "Preset:Name" (sINI)
		Goo1.AuxVarSetStr "CharPre_Cache" (sName) 0
		sMsgBox = Sv_Construct "^%z^%z: %z%r%r" sPreset (GetStringSetting "sName") (sName)

		; SPECIAL
		Goo1.AuxVarSetFlt "CharPre_Cache" (iStrength = GetINIFloat "Preset:Strength" (sINI)) 1
		Goo1.AuxVarSetFlt "CharPre_Cache" (iPerception = GetINIFloat "Preset:Perception" (sINI)) 2
		Goo1.AuxVarSetFlt "CharPre_Cache" (iEndurance = GetINIFloat "Preset:Endurance" (sINI)) 3
		Goo1.AuxVarSetFlt "CharPre_Cache" (iCharisma = GetINIFloat "Preset:Charisma" (sINI)) 4
		Goo1.AuxVarSetFlt "CharPre_Cache" (iIntelligence = GetINIFloat "Preset:Intelligence" (sINI)) 5
		Goo1.AuxVarSetFlt "CharPre_Cache" (iAgility = GetINIFloat "Preset:Agility" (sINI)) 6
		Goo1.AuxVarSetFlt "CharPre_Cache" (iLuck = GetINIFloat "Preset:Luck" (sINI)) 7
		sMsgBox += Sv_Construct "%z%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%r" (GetStringSetting "sStatsSpecial") (AVStringC 5 1) iStrength (AVStringC 6 1) iPerception (AVStringC 7 1) iEndurance (AVStringC 8 1) iCharisma (AVStringC 9 1) iIntelligence (AVStringC 10 1) iAgility (AVStringC 11 1) iLuck

		; Tag skills
		sMsgBox += "Tag Skills:"
		aStat = Sv_Split (GetINIString "Preset:TagSkills" (sINI)) "|"
		iLoop = 0
		while iLoop < 3
			Goo1.AuxVarSetStr "CharPre_Cache" (aStat[iLoop]) (iLoop + 8)
			sMsgBox += Sv_Construct "%r> %z" (AVStringC (StringToActorValue (aStat[iLoop])) 1)
			iLoop += 1
		loop

		; Traits
		while Goo1.AuxVarType "CharPre_Cache" 11 == 4
			Goo1.AuxVarErase "CharPre_Cache" 11
		loop
		sMsgBox += Sv_Construct "%r%rTraits:"
		if eval (GetINIString "Preset:Traits" (sINI) != "0")
			aStat = Sv_Split (GetINIString "Preset:Traits" (sINI)) "|"
			if aStat
				iLoop = 11
				foreach aForE <- aStat
					if eval IsFormValid (rPerk = EditorIDToFormID (*aForE))
						Goo1.AuxVarSetStr "CharPre_Cache" (*aForE) iLoop
						if iLoop < 15
							sMsgBox += Sv_Construct "%r> %n" rPerk
						elseif iLoop == 15
							sMsgBox += Sv_Construct "%r> %g more traits..." ((Ar_Size aStat) - (iLoop - 11))
						endif
						iLoop += 1
					else
						PrintC "Ignoring Unknown Trait: %z" (*aForE)
					endif
				loop
				if eval iLoop == 11
					sMsgBox += Sv_Construct "%r> %z" (GetStringSetting "sNone")
				endif
			endif
		else
			sMsgBox += Sv_Construct "%r> %z" (GetStringSetting "sNone")
		endif

		; Show preset message box
		MessageBoxExAlt (CompileScript "CharacterBuildPresets\VerifySpecial.gek") "%z|%z|%z" sMsgBox (GetStringSetting "sLoad") (GetStringSetting "sCancel")

	; Random preset
	elseif iButton == Goo1.AuxVarGetFlt "*CharPre_Flt" 1

		; Preset/character name
		aStat = Ar_List "Evil", "Good", "Neutral"
		sName = GetStringSetting (Sv_Construct "SKarmicTitle%z%02.0f" (aStat[(GetRandomInRange 0 3)]) (GetRandomInRange 1 31))
		Goo1.AuxVarSetStr "CharPre_Cache" (sName) 0
		sMsgBox = Sv_Construct "^%z^%z: %z%r%r" (GetStringSetting "sRandom") (GetStringSetting "sName") (sName)

		; SPECIAL
		aStat = Ar_Map 5::1, 6::1, 7::1, 8::1, 9::1, 10::1, 11::1
		iLoop = Goo1.AuxVarGetFlt "*CharPre_Flt" 0
		while iLoop > 0
			iRand = GetRandomInRange 5 12
			if eval aStat[iRand] < 10
				aStat[iRand] = aStat[iRand] + 1
				iLoop -= 1
			endif
		loop
		Goo1.AuxVarSetFlt "CharPre_Cache" (iStrength = aStat[5]) 1
		Goo1.AuxVarSetFlt "CharPre_Cache" (iPerception = aStat[6]) 2
		Goo1.AuxVarSetFlt "CharPre_Cache" (iEndurance = aStat[7]) 3
		Goo1.AuxVarSetFlt "CharPre_Cache" (iCharisma = aStat[8]) 4
		Goo1.AuxVarSetFlt "CharPre_Cache" (iIntelligence = aStat[9]) 5
		Goo1.AuxVarSetFlt "CharPre_Cache" (iAgility = aStat[10]) 6
		Goo1.AuxVarSetFlt "CharPre_Cache" (iLuck = aStat[11]) 7
		sMsgBox += Sv_Construct "%z%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%z: %g%r%r" (GetStringSetting "sStatsSpecial") (AVStringC 5 1) iStrength (AVStringC 6 1) iPerception (AVStringC 7 1) iEndurance (AVStringC 8 1) iCharisma (AVStringC 9 1) iIntelligence (AVStringC 10 1) iAgility (AVStringC 11 1) iLuck

		; Tag skills
		aStat = Ar_List 0, 0, 0
		iLoop = 0
		iPatch = GetOptionalPatch "bEnableBigGunsSkill"

		while iLoop < 3
			iRand = GetRandomInRange 32 46
			if eval (iRand != 33 || iPatch == 1) && (Ar_Find iRand, aStat) == Ar_BadNumericIndex
				aStat[iLoop] = iRand
				iLoop += 1
			endif
		loop
		aStat = Ar_Sort aStat
		Goo1.AuxVarSetStr "CharPre_Cache" (AVStringC (aStat[0])) 8
		Goo1.AuxVarSetStr "CharPre_Cache" (AVStringC (aStat[1])) 9
		Goo1.AuxVarSetStr "CharPre_Cache" (AVStringC (aStat[2])) 10
		sMsgBox += Sv_Construct "Tag Skills:%r> %z%r> %z%r> %z%r%r" (AVStringC (aStat[0]) 1) (AVStringC (aStat[1]) 1) (AVStringC (aStat[2]) 1)

		; Traits
		while Goo1.AuxVarType "CharPre_Cache" 11 == 4
			Goo1.AuxVarErase "CharPre_Cache" 11
		loop
		sMsgBox += "Traits:"
		iSize = GetRandomInRange (Goo1.AuxVarGetFlt "*CharPre_INI" 1) ((Goo1.AuxVarGetFlt "*CharPre_INI" 0) + 1)
		if iSize > 0
			aStat = GetLoadedTypeArray 86
			aStat = Ar_Filter aStat ({array_var aIter} => GetPerkFlag (*aIter) 0)
			aNames = Ar_Construct "array"
			iLoop = 0
			while iLoop < iSize
				iRand = GetRandomInRange 0 (Ar_Size aStat)
				Goo1.AuxVarSetStr "CharPre_Cache" (GetEditorID (aStat[iRand])) (iLoop + 11)
				Ar_Append aNames (LNGetName (aStat[iRand]))
				Ar_Erase aStat iRand
				iLoop += 1
			loop
			aNames = Ar_Sort aNames
			iLoop = 0
			while iLoop < iSize
				if iLoop < 4
					sMsgBox += Sv_Construct "%r> %z" (aNames[iLoop])
					iLoop += 1
				else
					sMsgBox += Sv_Construct "%r> %g more traits..." (iSize - iLoop)
					break
				endif
			loop
		else
			sMsgBox += Sv_Construct "%r> %z" (GetStringSetting "sNone")
		endif

		; Show preset message box
		Goo1.AuxVarSetFlt "*CharPre_Flt" 1 2
		MessageBoxExAlt (CompileScript "CharacterBuildPresets\LoadINI.gek") "%z|%z|%z|%z" sMsgBox (GetStringSetting "sLoad") (GetStringSetting "sRSMRandomize") (GetStringSetting "sCancel")

	endif

	Sv_Destruct sINI sMsgBox sPreset sName
	aStat = aNames = aForE = Ar_Null

end